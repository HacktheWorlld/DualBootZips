### LICENSE:
#
# Copyright (C) 2011 Ahmad Amarullah ( http://amarullz.com/ )
# Copyright (C) 2013-2015 Xiao-Long Chen <chenxiaolong@cxl.epac.to>
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

ini_set("rom_name",    "Multi-Boot Utilities");
ini_set("rom_version", "4.0-snapshot2");
ini_set("rom_author",  "Xiao-Long Chen");
ini_set("rom_device",  "");
ini_set("rom_date",    "2015-04-30");

# English only for right now
loadlang("langs/en.lang");

theme("chenxiaolong");

#fontresload( "0", "ttf/Roboto-Regular.ttf;ttf/DroidSansFallback.ttf;", "12" );
#fontresload( "1", "ttf/Roboto-Regular.ttf;ttf/DroidSansFallback.ttf;", "14" );
fontresload( "0", "ttf/Roboto-Regular.ttf", "12" );
fontresload( "1", "ttf/Roboto-Regular.ttf", "14" );

setvar(
  "sysinfo",
  "<@center><b>Your device:</b></@>\n\n" +

  "Model\t\t\t: "      + "<b><#selectbg_g>" + sysprop("ro.product.model")        + "</#></b>\n"   +
  "Name\t\t\t: "       + "<b><#selectbg_g>" + sysprop("ro.product.name")         + "</#></b>\n"   +
  "Device Name\t\t: "  + "<b><#selectbg_g>" + sysprop("ro.product.device")       + "</#></b>\n"   +
  "Board Name\t\t: "   + "<b><#selectbg_g>" + sysprop("ro.product.board")        + "</#></b>\n"   +
  "Manufacturer\t\t: " + "<b><#selectbg_g>" + sysprop("ro.product.manufacturer") + "</#></b>\n\n" +

  "/system Size\t\t: " + "<b><#selectbg_g>" + getdisksize("/system","m") + " MB" + "</#></b>\n"   +
    "\tFree\t\t\t: "   + "<b><#selectbg_g>" + getdiskfree("/system","m") + " MB" + "</#></b>\n\n" +
  "/data Size\t\t: "   + "<b><#selectbg_g>" + getdisksize("/data","m")   + " MB" + "</#></b>\n"   +
    "\tFree\t\t\t: "   + "<b><#selectbg_g>" + getdiskfree("/data","m")   + " MB" + "</#></b>\n\n" +
  "/cache Size\t\t: "  + "<b><#selectbg_g>" + getdisksize("/cache","m")  + " MB" + "</#></b>\n"   +
    "\tFree\t\t\t: "   + "<b><#selectbg_g>" + getdiskfree("/cache","m")  + " MB" + "</#></b>\n\n"+
  "Internal SD Size\t: " + "<b><#selectbg_g>" + getdisksize("/sdcard","m") + " MB" + "</#></b>\n" +
    "\tFree\t\t\t: "   + "<b><#selectbg_g>" + getdiskfree("/sdcard","m") + " MB" + "</#></b>\n\n" +
  "External SD Size\t: " + "<b><#selectbg_g>" + getdisksize("/external_sd","m") + " MB" + "</#></b>\n" +
    "\tFree\t\t\t: "   + "<b><#selectbg_g>" + getdiskfree("/external_sd","m") + " MB" + "</#></b>\n\n" +

  ""
);

viewbox(
    "<~welcome.title>",
    "<~welcome.text1> <b>"     +
      ini_get("rom_name")      +
      "</b> <~common.for> <b>" +
      ini_get("rom_device")    +
      "</b>.\n\n"              +

    "<~welcome.text2>\n\n"+

      "  <~welcome.version>\t\t: <b><#selectbg_g>"+ini_get("rom_version")+"</#></b>\n"+
      "  <~welcome.updated>\t\t: <b><#selectbg_g>"+ini_get("rom_date")+"</#></b>\n\n\n" +
      getvar("sysinfo"),

    "@welcome"
);

gotolabel("main_menu");

menubox(
    "<~operations.title>",
    "<~operations.desc>",
    "@personalize",
    "operations.prop",

    "Reboot",                  "Reboot your device",                      "@alert",
    "Exit",                    "Exit to recovery",                        "@install",
    "Primary",                 "",                                        "@default",
    "Secondary",               "",                                        "@default",
    "Multi slot 1",            "",                                        "@default",
    "Multi slot 2",            "",                                        "@default",
    "Multi slot 3",            "",                                        "@default",
    "Migrate ROMs",            "Needed for patcher >= 8.0.0.r436",        "@default",
    "Save last_kmsg log",      "Copies /proc/last_kmsg to internal SD",   "@default",
    "Save recovery log",       "Copies /tmp/recovery.log to internal SD", "@default"
);

# Reboot
if prop("operations.prop","selected") == "1" then
  if confirm("Reboot",
             "Are you sure want to reboot your device?",
             "@alert"
    ) == "yes"
  then
    reboot("now");
  else
    goto("main_menu");
  endif;
endif;

# Exit
if prop("operations.prop","selected") == "2" then
  if confirm("Exit",
             "Are you sure want to exit the Installer?",
             "@alert"
    )=="yes"
  then
    exit("");
  else
    goto("main_menu");
  endif;
endif;

setvar("partconfigid", "NONE");

if prop("operations.prop","selected") == "3" then
  setvar("partconfigid", "primary");
  setvar("partconfig", "primary");
endif;

if prop("operations.prop","selected") == "4" then
  setvar("partconfigid", "secondary");
  setvar("partconfig", "secondary");
endif;

if prop("operations.prop","selected") == "5" then
  setvar("partconfigid", "multi-slot-1");
  setvar("partconfig", "multi slot 1");
endif;

if prop("operations.prop","selected") == "6" then
  setvar("partconfigid", "multi-slot-2");
  setvar("partconfig", "multi slot 2");
endif;

if prop("operations.prop","selected") == "7" then
  setvar("partconfigid", "multi-slot-3");
  setvar("partconfig", "multi slot 3");
endif;

# Is there any way to not list it like this?
if prop("operations.prop","selected") == "3" ||
   prop("operations.prop","selected") == "4" ||
   prop("operations.prop","selected") == "5" ||
   prop("operations.prop","selected") == "6" ||
   prop("operations.prop","selected") == "7" then

  menubox(
      "<~operations.title> (" + getvar("partconfig") + ")",
      "<~operations.desc> (" + getvar("partconfig") + ")",
      "@personalize",
      "romoperations.prop",

      "Switch to",                    "", "@default",
      "Wipe /system",                 "", "@default",
      "Wipe /cache",                  "", "@default",
      "Wipe /data",                   "", "@default",
      "Wipe dalvik-cache",            "", "@default",
      "Wipe /cache and dalvik-cache", "", "@default",
      "Wipe all",                     "", "@default"
  );

  if prop("romoperations.prop","selected") == "1" then
    setvar("operation_argument", "switch-to-" + getvar("partconfigid"));
    setvar("operation_title", "Switch to " + getvar("partconfig"));
    setvar("operation_futuretense", "switch to " + getvar("partconfig"));
    setvar("operation_pasttense", "switched to " + getvar("partconfig"));
  endif;

  if prop("romoperations.prop","selected") == "2" then
    setvar("operation_argument", "wipe-system-" + getvar("partconfigid"));
    setvar("operation_title", "Wipe /system for " + getvar("partconfig"));
    setvar("operation_futuretense", "wipe /system for " + getvar("partconfig"));
    setvar("operation_pasttense", "wiped /system for " + getvar("partconfig"));
  endif;

  if prop("romoperations.prop","selected") == "3" then
    setvar("operation_argument", "wipe-cache-" + getvar("partconfigid"));
    setvar("operation_title", "Wipe /cache for " + getvar("partconfig"));
    setvar("operation_futuretense", "wipe /cache for " + getvar("partconfig"));
    setvar("operation_pasttense", "wiped /cache for " + getvar("partconfig"));
  endif;

  if prop("romoperations.prop","selected") == "4" then
    setvar("operation_argument", "wipe-data-" + getvar("partconfigid"));
    setvar("operation_title", "Wipe /data for " + getvar("partconfig"));
    setvar("operation_futuretense", "wipe /data for " + getvar("partconfig"));
    setvar("operation_pasttense", "wiped /data for " + getvar("partconfig"));
  endif;

  if prop("romoperations.prop","selected") == "5" then
    setvar("operation_argument", "wipe-dalvik-cache-" + getvar("partconfigid"));
    setvar("operation_title", "Wipe dalvik-cache for " + getvar("partconfig"));
    setvar("operation_futuretense", "wipe dalvik-cache for " + getvar("partconfig"));
    setvar("operation_pasttense", "wiped dalvik-cache for " + getvar("partconfig"));
  endif;

  if prop("romoperations.prop","selected") == "6" then
    setvar("operation_argument", "wipe-caches-" + getvar("partconfigid"));
    setvar("operation_title", "Wipe /cache and dalvik-cache for " + getvar("partconfig"));
    setvar("operation_futuretense", "wipe /cache and dalvik-cache for " + getvar("partconfig"));
    setvar("operation_pasttense", "wiped /cache and dalvik-cache for " + getvar("partconfig"));
  endif;

  if prop("romoperations.prop","selected") == "7" then
    setvar("operation_argument", "wipe-all-" + getvar("partconfigid"));
    setvar("operation_title", "Wipe everything for " + getvar("partconfig"));
    setvar("operation_futuretense", "wipe everything for " + getvar("partconfig"));
    setvar("operation_pasttense", "wiped everything for " + getvar("partconfig"));
  endif;
endif;

# Migrate ROMs
if prop("operations.prop","selected") == "8" then
  setvar("operation_argument", "migrate-roms");
  setvar("operation_title", "Migrate ROMs");
  setvar("operation_futuretense", "migrate multiboot ROMs' /data");
  setvar("operation_pasttense", "migrated multiboot ROMs' /data");
endif;

# Save last_kmsg log
if prop("operations.prop","selected") == "9" then
  setvar("operation_argument", "save-last-kmsg");
  setvar("operation_title", "Save last_kmsg");
  setvar("operation_futuretense", "save the last_kmsg kernel log");
  setvar("operation_pasttense", "saved the last_kmsg kernel log");
endif;

# Save recovery log
if prop("operations.prop","selected") == "10" then
  setvar("operation_argument", "save-recovery-log");
  setvar("operation_title", "Save recovery log");
  setvar("operation_futuretense", "save the recovery log");
  setvar("operation_pasttense", "saved the recovery log");
endif;

ini_set("text_next", "Continue");
ini_set("icon_next", "@installbutton");

viewbox(getvar("operation_title"),
        "The wizard will now " + getvar("operation_futuretense") + ".\n\n" +
        "Press <b>Continue</b> to begin.\n\n" +
        "If you want to choose another operation, press <b>Back</b>. To exit, press the menu button and choose Quit Installation.",
        "@install"
);

ini_set("text_next", "Next");
ini_set("icon_next", "@next");

pleasewait("Executing Shell...");

setvar("exitcode",
       resexec("multibootutils.sh", getvar("operation_argument"))
);

textbox(getvar("operation_title"),
        "Operation complete",
        "@default",
        getvar("exec_buffer") +
        "\n\n<b>EXIT CODE: "  +
        getvar("exitcode")    +
        "</b>"
);

if getvar("exitcode") == 0 then
  setvar("completion_message", "Successfully " + getvar("operation_pasttense"));
  setvar("completion_icon", "@finish");
else
  setvar("completion_message",
         "Failed to " + getvar("operation_futuretense") + "\n\n" +
         "Please save the recovery log from the main menu and report the issue.");
  setvar("completion_icon", "@alert");
endif;

ini_set("text_next", "Finish");
ini_set("icon_next", "@finish");

viewbox("Operation Completed",
        getvar("completion_message") + "\n\n" +
        "Press Finish to go back to the main menu.",
        getvar("completion_icon")
);

goto("main_menu");
