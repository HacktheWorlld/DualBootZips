### LICENSE:
#
# Copyright (C) 2011 Ahmad Amarullah ( http://amarullz.com/ )
# Copyright (C) 2013 Xiao-Long Chen <chenxiaolong@cxl.epac.to>
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

ini_set("rom_name",    "Multi-Boot Utilities");
ini_set("rom_version", "1.0");
ini_set("rom_author",  "Xiao-Long Chen");
ini_set("rom_device",  "Samsung Galaxy S4");
ini_set("rom_date",    "2013-09-20");

anisplash(
    2,
    "splash/a1", 500,
    "splash/a2", 30,
    "splash/a3", 30,
    "splash/a4", 30,
    "splash/a5", 30,
    "splash/a6", 30
);

# English only for right now
loadlang("langs/en.lang");

#fontresload( "0", "ttf/Roboto-Regular.ttf;ttf/DroidSansFallback.ttf;", "12" );
#fontresload( "1", "ttf/Roboto-Regular.ttf;ttf/DroidSansFallback.ttf;", "14" );
fontresload( "0", "ttf/Roboto-Regular.ttf", "12" );
fontresload( "1", "ttf/Roboto-Regular.ttf", "14" );

#pleasewait("Getting device information ...");

setvar(
  "sysinfo",
  "<@center><b>Your device:</b></@>\n\n" +

  "Model\t\t\t: "      + "<b><#selectbg_g>" + sysprop("ro.product.model")        + "</#></b>\n"   +
  "Name\t\t\t: "       + "<b><#selectbg_g>" + sysprop("ro.product.name")         + "</#></b>\n"   +
  "Device Name\t\t: "  + "<b><#selectbg_g>" + sysprop("ro.product.device")       + "</#></b>\n"   +
  "Board Name\t\t: "   + "<b><#selectbg_g>" + sysprop("ro.product.board")        + "</#></b>\n"   +
  "Manufacturer\t\t: " + "<b><#selectbg_g>" + sysprop("ro.product.manufacturer") + "</#></b>\n\n" +

  "/system Size\t\t: " + "<b><#selectbg_g>" + getdisksize("/system","m") + " MB" + "</#></b>\n"   +
    "\tFree\t\t\t: "   + "<b><#selectbg_g>" + getdiskfree("/system","m") + " MB" + "</#></b>\n\n" +
  "/data Size\t\t: "   + "<b><#selectbg_g>" + getdisksize("/data","m")   + " MB" + "</#></b>\n"   +
    "\tFree\t\t\t: "   + "<b><#selectbg_g>" + getdiskfree("/data","m")   + " MB" + "</#></b>\n\n" +
  "/cache Size\t\t: "  + "<b><#selectbg_g>" + getdisksize("/cache","m")  + " MB" + "</#></b>\n"   +
    "\tFree\t\t\t: "   + "<b><#selectbg_g>" + getdiskfree("/cache","m")  + " MB" + "</#></b>\n\n"+
  "Internal SD Size\t: " + "<b><#selectbg_g>" + getdisksize("/sdcard","m") + " MB" + "</#></b>\n" +
    "\tFree\t\t\t: "   + "<b><#selectbg_g>" + getdiskfree("/sdcard","m") + " MB" + "</#></b>\n\n" +
  "External SD Size\t: " + "<b><#selectbg_g>" + getdisksize("/external_sd","m") + " MB" + "</#></b>\n" +
    "\tFree\t\t\t: "   + "<b><#selectbg_g>" + getdiskfree("/external_sd","m") + " MB" + "</#></b>\n\n" +

  ""
);

viewbox(
    "<~welcome.title>",
    "<~welcome.text1> <b>"     +
      ini_get("rom_name")      +
      "</b> <~common.for> <b>" +
      ini_get("rom_device")    +
      "</b>.\n\n"              +

    "<~welcome.text2>\n\n"+

      "  <~welcome.version>\t\t: <b><#selectbg_g>"+ini_get("rom_version")+"</#></b>\n"+
      "  <~welcome.updated>\t\t: <b><#selectbg_g>"+ini_get("rom_date")+"</#></b>\n\n\n" +
      getvar("sysinfo"),

    "@welcome"
);

textbox(
    "<~changelog.title>",
    "<~changelog.desc>",
    "@update",
    resread("changelog.txt")
);

gotolabel("main_menu");

menubox(
    "<~operations.title>",
    "<~operations.desc>",
    "@personalize",
    "operations.prop",

    "Reboot",                      "Reboot your device",                                       "@alert",
    "Exit",                        "Exit to recovery",                                         "@install",
    "Switch to primary ROM",       "",                                                         "@default",
    "Switch to secondary ROM",     "",                                                         "@default",
    "Wipe primary /cache",         "Deletes everything from /cache, except for /cache/dual",   "@default",
    "Wipe secondary /cache",       "Deletes /cache/dual",                                      "@default",
    "Wipe primary dalvik cache",   "Deletes /data/dalvik-cache",                               "@default",
    "Wipe secondary dalvik cache", "Deletes /data/dual/dalvik-cache",                          "@default",
    "Wipe primary /system",        "Deletes everything from /system, except for /system/dual", "@default",
    "Wipe secondary /system",      "Deletes /system/dual",                                     "@default",
    "Wipe primary /data",          "Deletes everything from /data, except for /data/dual and internal SD", "@default",
    "Wipe secondary /data",        "Deletes /data/dual",                                       "@default",
    "Save last_kmsg log",          "Copies /proc/last_kmsg to internal SD",                    "@default",
    "Save recovery log",           "Copies /tmp/recovery.log to internal SD",                  "@default"
);

# Reboot
if prop("operations.prop","selected") == "1" then
  if confirm("Reboot",
             "Are you sure want to reboot your device?",
             "@alert"
    ) == "yes"
  then
    reboot("now");
  else
    goto("main_menu");
  endif;
endif;

# Exit
if prop("operations.prop","selected") == "2" then
  if confirm("Exit",
             "Are you sure want to exit the Installer?",
             "@alert"
    )=="yes"
  then
    exit("");
  else
    goto("main_menu");
  endif;
endif;

# Switch to primary ROM
if prop("operations.prop","selected") == "3" then
  setvar("operation_argument", "force-boot-primary");
  setvar("operation_title", "Switch to primary ROM");
  setvar("operation_futuretense", "switch to the primary ROM");
  setvar("operation_pasttense", "switched to the primary ROM");
endif;

# Switch to secondary ROM
if prop("operations.prop","selected") == "4" then
  setvar("operation_argument", "force-boot-secondary");
  setvar("operation_title", "Switch to secondary ROM");
  setvar("operation_futuretense", "switch to the secondary ROM");
  setvar("operation_pasttense", "switched to the secondary ROM");
endif;

# Wipe primary /cache
if prop("operations.prop","selected") == "5" then
  setvar("operation_argument", "wipe-primary-cache");
  setvar("operation_title", "Wipe primary /cache");
  setvar("operation_futuretense", "wipe the primary /cache");
  setvar("operation_pasttense", "wiped the primary /cache");
endif;

# Wipe secondary /cache
if prop("operations.prop","selected") == "6" then
  setvar("operation_argument", "wipe-secondary-cache");
  setvar("operation_title", "Wipe secondary /cache");
  setvar("operation_futuretense", "wipe the secondary /cache");
  setvar("operation_pasttense", "wiped the secondary /cache");
endif;

# Wipe primary dalvik-cache
if prop("operations.prop","selected") == "7" then
  setvar("operation_argument", "wipe-primary-dalvik-cache");
  setvar("operation_title", "Wipe primary dalvik cache");
  setvar("operation_futuretense", "wipe the primary dalvik cache");
  setvar("operation_pasttense", "wiped the primary dalvik cache");
endif;

# Wipe secondary dalvik-cache
if prop("operations.prop","selected") == "8" then
  setvar("operation_argument", "wipe-secondary-dalvik-cache");
  setvar("operation_title", "Wipe secondary dalvik cache");
  setvar("operation_futuretense", "wipe the secondary dalvik cache");
  setvar("operation_pasttense", "wiped the secondary dalvik cache");
endif;

# Wipe primary /system
if prop("operations.prop","selected") == "9" then
  setvar("operation_argument", "wipe-primary-system");
  setvar("operation_title", "Wipe primary /system");
  setvar("operation_futuretense", "wipe the primary /system");
  setvar("operation_pasttense", "wiped the primary /system");
endif;

# Wipe secondary /system
if prop("operations.prop","selected") == "10" then
  setvar("operation_argument", "wipe-secondary-system");
  setvar("operation_title", "Wipe secondary /system");
  setvar("operation_futuretense", "wipe the secondary /system");
  setvar("operation_pasttense", "wiped the secondary /system");
endif;

# Wipe primary /data
if prop("operations.prop","selected") == "11" then
  setvar("operation_argument", "wipe-primary-data");
  setvar("operation_title", "Wipe primary /data");
  setvar("operation_futuretense", "wipe the primary /data");
  setvar("operation_pasttense", "wiped the primary /data");
endif;

# Wipe secondary /data
if prop("operations.prop","selected") == "12" then
  setvar("operation_argument", "wipe-secondary-data");
  setvar("operation_title", "Wipe secondary /data");
  setvar("operation_futuretense", "wipe the secondary /data");
  setvar("operation_pasttense", "wiped the secondary /data");
endif;

# Save last_kmsg log
if prop("operations.prop","selected") == "13" then
  setvar("operation_argument", "save-last-kmsg");
  setvar("operation_title", "Save last_kmsg");
  setvar("operation_futuretense", "save the last_kmsg kernel log");
  setvar("operation_pasttense", "saved the last_kmsg kernel log");
endif;

# Save recovery log
if prop("operations.prop","selected") == "14" then
  setvar("operation_argument", "save-recovery-log");
  setvar("operation_title", "Save recovery log");
  setvar("operation_futuretense", "save the recovery log");
  setvar("operation_pasttense", "saved the recovery log");
endif;

ini_set("text_next", "Continue");
ini_set("icon_next", "@installbutton");

viewbox(getvar("operation_title"),
        "The wizard will now " + getvar("operation_futuretense") + ".\n\n" +
        "Press <b>Continue</b> to begin.\n\n" +
        "If you want to choose another operation, press <b>Back</b>. To exit, press the menu button and choose Quit Installation.",
        "@install"
);

ini_set("text_next", "Next");
ini_set("icon_next", "@next");

pleasewait("Executing Shell...");

setvar("exitcode",
       resexec("multibootutils.sh", getvar("operation_argument"))
);

textbox(getvar("operation_title"),
        "Operation complete",
        "@default",
        getvar("exec_buffer") +
        "\n\n<b>EXIT CODE: "  +
        getvar("exitcode")    +
        "</b>"
);

if getvar("exitcode") == 0 then
  setvar("completion_message", "Successfully " + getvar("operation_pasttense"));
  setvar("completion_icon", "@finish");
else
  setvar("completion_message",
         "Failed to " + getvar("operation_futuretense") + "\n\n" +
         "Please save the recovery log from the main menu and report the issue.");
  setvar("completion_icon", "@alert");
endif;

ini_set("text_next", "Finish");
ini_set("icon_next", "@finish");

viewbox("Operation Completed",
        getvar("completion_message") + "\n\n" +
        "Press Finish to go back to the main menu.",
        getvar("completion_icon")
);

goto("main_menu");